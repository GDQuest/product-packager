Import("env")
import scons_helper as helper

helper.success_log("Attempting standard build")

env["BUILD_DIR"] = "build/"
env["DIST_DIR"] = "dist/"
VariantDir(env["BUILD_DIR"], env["src"], duplicate=False)

HTMLBuilder = Builder(action=helper.process_markdown_file_in_place,
        suffix='.html',
        src_suffix='.md',
        single_source=1,
        )

env['BUILDERS']["HTMLBuilder"] = HTMLBuilder

GDBuilder = Builder(action=helper.bundle_godot_project,
        suffix='.zip',
        )

env['BUILDERS']["GDBuilder"] = GDBuilder

src_path = helper.pathlib.Path(env["src"]) / "content"
src_contents = helper.content_introspection(env["src"])

images = []
for folder in src_contents:
    images.extend(
        helper.glob_extensions(folder, ["*.png", "*.jpg", "*.svg"])
    )
ilist = []
for image_path in images:
    ilist.append(Install(env["BUILD_DIR"] + image_path.relative_to(src_path).parent.as_posix(), image_path.as_posix()))

videos = []
for folder in src_contents:
    videos.extend(
        helper.glob_extensions(folder, ["*.mp4", "*.jpg"])
    )
vlist = []
for video_path in videos:
    vlist.append(Install(env["BUILD_DIR"] + video_path.relative_to(src_path).parent.as_posix(), video_path.as_posix()))

markdown_files = []
for folder in src_contents:
    markdown_files.extend(helper.glob_extensions(folder, ["*.md"]))
for markdown_path in markdown_files:
    targ = env["BUILD_DIR"] + markdown_path.relative_to(src_path).as_posix()
    Install(env["BUILD_DIR"] + markdown_path.relative_to(src_path).parent.as_posix(), markdown_path.as_posix())
    build_html_file = env.HTMLBuilder(targ)
    env.AddPostAction(build_html_file, Delete(targ))
    for image in ilist:
        Depends(build_html_file, image)
    for video in vlist:
        Depends(build_html_file, video)

for folder in helper.get_godot_folders(env["src"]):
    gd_name = helper.get_godot_filename(folder)
    env.GDBuilder(env["BUILD_DIR"] + gd_name + ".zip", folder + "/project.godot")