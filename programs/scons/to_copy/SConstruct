import os
from pathlib import Path

import dotenv
from SCons.Script import Environment, Export, Dir


def move_mavenseed_node_guides_outside_folders(target, source, env):
    """Moves node guides to the parent directory and removes the folder
    containing them"""
    path: Path = Path(source[0].abspath)
    directory: Path = path.parent
    path.rename(path.parent.parent / path.name)
    directory.rmdir()


env = Environment()
dotenv.load_dotenv()

env["SRC_DIR"] = Dir(".")
env["BUILD_DIR"] = env["SRC_DIR"].Dir("build")
env["DIST_DIR"] = env["SRC_DIR"].Dir("dist")
env["PRODUCT_PACKAGER_DIR"] = Dir(os.environ.get("PATH_TO_PRODUCT_PACKAGER"))

if os.environ.get("PYTHONUSERBASE", None):
    env["ENV"]["PYTHONUSERBASE"] = os.environ["PYTHONUSERBASE"]

Export("env")
mavenseed_files = env.SConscript(env["PRODUCT_PACKAGER_DIR"].File("SConstruct"))

if env.GetOption("mavenseed"):
    MavenseedMoveBuilder = env.Builder(
        action=move_mavenseed_node_guides_outside_folders,
        suffix=".html",
        src_suffix=".html",
        single_source=1,
    )
    env["BUILDERS"]["MavenseedMoveBuilder"] = MavenseedMoveBuilder
    mavenseed_folder = env["DIST_DIR"].Dir("mavenseed")
    IGNORED_FOLDERS = ["00.introduction"]
    for path in mavenseed_files:
        if path.Dir(".").name in IGNORED_FOLDERS:
            continue
        moved_file = mavenseed_folder.File(path.name)
        mavenseed_flatten_output = env.MavenseedMoveBuilder(moved_file, path)
        env.Depends(mavenseed_flatten_output, path)
