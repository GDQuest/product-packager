#!/usr/bin/env python

Import("env")
Import("env_modules")

env_mbed_tls = env_modules.Clone()

# Thirdparty source files

thirdparty_obj = []

if env["builtin_mbedtls"]:
    thirdparty_sources = [
        "aes.c",
        "aesni.c",
        "arc4.c",
        "aria.c",
        "asn1parse.c",
        "asn1write.c",
        "base64.c",
        "bignum.c",
        "blowfish.c",
        "camellia.c",
        "ccm.c",
        "certs.c",
        "chacha20.c",
        "chachapoly.c",
        "cipher.c",
        "cipher_wrap.c",
        "cmac.c",
        "ctr_drbg.c",
        "constant_time.c",
        "debug.c",
        "des.c",
        "dhm.c",
        "ecdh.c",
        "ecdsa.c",
        "ecjpake.c",
        "ecp.c",
        "ecp_curves.c",
        "entropy.c",
        "entropy_poll.c",
        "error.c",
        "gcm.c",
        "havege.c",
        "hkdf.c",
        "hmac_drbg.c",
        "md2.c",
        "md4.c",
        "md5.c",
        "md.c",
        "memory_buffer_alloc.c",
        "mps_reader.c",
        "mps_trace.c",
        "net_sockets.c",
        "nist_kw.c",
        "oid.c",
        "padlock.c",
        "pem.c",
        "pk.c",
        "pkcs11.c",
        "pkcs12.c",
        "pkcs5.c",
        "pkparse.c",
        "pk_wrap.c",
        "pkwrite.c",
        "platform.c",
        "platform_util.c",
        "poly1305.c",
        "ripemd160.c",
        "rsa.c",
        "rsa_internal.c",
        "sha1.c",
        "sha256.c",
        "sha512.c",
        "ssl_cache.c",
        "ssl_ciphersuites.c",
        "ssl_cli.c",
        "ssl_cookie.c",
        "ssl_msg.c",
        "ssl_srv.c",
        "ssl_ticket.c",
        "ssl_tls.c",
        "ssl_tls13_keys.c",
        "threading.c",
        "timing.c",
        "version.c",
        "version_features.c",
        "x509.c",
        "x509_create.c",
        "x509_crl.c",
        "x509_crt.c",
        "x509_csr.c",
        "x509write_crt.c",
        "x509write_csr.c",
        "xtea.c",
    ]

    thirdparty_dir = "#thirdparty/mbedtls/library/"
    thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

    env_mbed_tls.Prepend(CPPPATH=["#thirdparty/mbedtls/include/"])
    env_mbed_tls.Append(
        CPPDEFINES=[("MBEDTLS_CONFIG_FILE", '\\"thirdparty/mbedtls/include/godot_module_mbedtls_config.h\\"')]
    )

    env_thirdparty = env_mbed_tls.Clone()
    env_thirdparty.disable_warnings()
    env_thirdparty.add_source_files(thirdparty_obj, thirdparty_sources)
    env_thirdparty.Depends(thirdparty_obj, "#thirdparty/mbedtls/include/godot_module_mbedtls_config.h")
    env.modules_sources += thirdparty_obj


# Godot source files

module_obj = []

env_mbed_tls.add_source_files(module_obj, "*.cpp")

if env["tests"]:
    env_mbed_tls.Append(CPPDEFINES=["TESTS_ENABLED"])
    env_mbed_tls.add_source_files(module_obj, "./tests/*.cpp")

env.modules_sources += module_obj

# Needed to force rebuilding the module files when the thirdparty library is updated.
env.Depends(module_obj, thirdparty_obj)
